plugins {
	id 'java'
	id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.quiltmc'
archivesBaseName = project.name
version = '1.0.2'

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
	mavenCentral()
	maven {
		name = "Quilt"
		url = 'https://maven.quiltmc.org/repository/release'
	}
}

dependencies {
	implementation 'org.quiltmc:tiny-mappings-parser:0.3.0'

	// use newer dependencies
	implementation ('com.google.code.gson:gson:2.8.5')
	implementation ('com.google.guava:guava:27.1-jre')

	implementation ('org.quiltmc:sponge-mixin:0.9.2+mixin.0.8.2') {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
		exclude module: 'gson'
	}
}

java {
	withSourcesJar()
	withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

// TODO: Spotless when convention plugins are done
// apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'

publishing {
	publications {
		plugin(MavenPublication) { publication ->
			groupId project.group
			artifactId project.archivesBaseName
			version project.version

			artifact sourcesJar
			artifact javadocJar
		}
	}

	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}
